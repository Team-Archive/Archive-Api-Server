plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    queryDslVersion = "5.0.0"
    testContainerVersion = "1.17.2"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'com.slack.api:slack-api-client:1.22.1'
    implementation 'io.sentry:sentry-logback:5.7.4'

    implementation 'mysql:mysql-connector-java'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    implementation "com.querydsl:querydsl-jpa:${queryDslVersion}"
    annotationProcessor(
            "javax.persistence:javax.persistence-api",
            "javax.annotation:javax.annotation-api",
            "com.querydsl:querydsl-apt:${queryDslVersion}:jpa")
    runtimeOnly 'com.h2database:h2'
    testImplementation 'org.springframework.security:spring-security-test'
    implementation 'io.rest-assured:rest-assured-all:4.5.1'
    testImplementation 'io.rest-assured:rest-assured:4.5.1'

    implementation project(':archive-domain')
}

allprojects { // 추후 root project 제거시 subproject 로 수정
    apply {
        plugin 'java'
        plugin 'io.spring.dependency-management'
        plugin 'org.springframework.boot'
    }

    group = 'site.archive'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-json'
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.mockito:mockito-inline:4.5.1'
        testImplementation "org.testcontainers:testcontainers:${testContainerVersion}"
        testImplementation "org.testcontainers:junit-jupiter:${testContainerVersion}"
        testImplementation "org.testcontainers:mysql:${testContainerVersion}"
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

configure(allprojects.findAll { it.name != 'archive-common' }) {
    dependencies {
        implementation project(':archive-common')
    }
}

// QueryDSL
sourceSets {
    main {
        java {
            srcDirs = ["$projectDir/src/main/java", "$projectDir/build/generated"]
        }
    }
}