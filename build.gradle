plugins {
    id 'org.springframework.boot' version '3.0.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'

    // for kotlin
    id 'org.jetbrains.kotlin.jvm' version '1.7.22'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.7.22'
    id "org.jetbrains.kotlin.plugin.jpa" version '1.7.22'
}

repositories {
    mavenCentral()
}

ext {
    queryDslVersion = "5.0.0"
    testContainerVersion = "1.17.2"
}

subprojects {
    apply {
        plugin 'org.springframework.boot'
        plugin 'io.spring.dependency-management'
        plugin 'java'
        plugin 'org.jetbrains.kotlin.jvm'
        plugin 'org.jetbrains.kotlin.plugin.spring'
        plugin 'org.jetbrains.kotlin.plugin.jpa'
    }

    group = 'site.archive'
    version = '2.1.0'
    sourceCompatibility = '17'

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-json'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-aop'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.mockito:mockito-inline:4.5.1'
        testImplementation "org.testcontainers:testcontainers:${testContainerVersion}"
        testImplementation "org.testcontainers:junit-jupiter:${testContainerVersion}"
        testImplementation "org.testcontainers:mysql:${testContainerVersion}"

        // for kotlin
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // for kotlin
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_17
            freeCompilerArgs = ["-Xjsr305=strict"]
        }
    }

    // for kotlin
    allOpen {
        annotation("javax.persistence.Entity")
        annotation("javax.persistence.MappedSuperclass")
        annotation("javax.persistence.Embeddable")
    }

}

configure(allprojects.findAll { it.name != 'archive-common' }) {
    dependencies {
        implementation project(':archive-common')
    }
}